WHITESPACE        = _{ " " | "\t" | "\r" | "\n" }
COMMENT           = _{ SingleLineComment }
SingleLineComment =  { "//" ~ (!"\n" ~ ANY)* }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

float = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

integer = @{ ASCII_DIGIT* }

boolean = { true | false }
true    = { ^"true" }
false   = { ^"false" }

glyph                = { ^"glyph" }
word                 = { ^"word" }
none                 = { ^"none" }
either               = { ^"either" | ^"both" }
basic                = { ^"basic" }
advanced             = { ^"advanced" }
direction_horizontal = { ^"horizontal" }
direction_vertical   = { ^"vertical" }
both                 = { ^"both" }

// Delimiter
delimiter = _{ ":" }

attributes = _{
    attr_padding
  | attr_width
  | attr_height
  | attr_max_width
  | attr_max_height
  | attr_size
  | attr_align
  | attr_align_x
  | attr_align_y
  | attr_text_color
  | attr_background
  | attr_border
  | attr_shadow
  | attr_padding
  | attr_spacing
  | attr_selected
  | attr_cell_size
  | attr_label
  | attr_clip
  | attr_toggled
  | attr_wrapping
  | attr_shaping
  | attr_direction
}

attr_padding = { ^"padding" ~ delimiter ~ (full | edge | uniform | padding_option_list | module) }

attr_width      = { ^"width" ~ delimiter ~ (length | pixels | module) }
attr_height     = { ^"height" ~ delimiter ~ (length | pixels | module) }
attr_max_width  = { ^"max-width" ~ delimiter ~ (pixels | module) }
attr_max_height = { ^"max-height" ~ delimiter ~ (pixels | module) }
attr_size       = { ^"size" ~ delimiter ~ (pixels | module) }
attr_cell_size  = { ^"cell-size" ~ delimiter ~ (pixels | module) }
attr_spacing    = { ^"spacing" ~ delimiter ~ (pixels | module) }
attr_align_x    = { ^"align-x" ~ delimiter ~ (horizontal | module) }
attr_align_y    = { ^"align-y" ~ delimiter ~ (vertical | module) }
attr_align      = { ^"align" ~ delimiter ~ (horizontal | vertical | module) }
attr_text_color = { (^"text-color" | ^"text-colour") ~ delimiter ~ (color_hex | option_color | module) }
attr_background = { (^"background" | ^"bg") ~ delimiter ~ (option_gradient | option_color | module) }
attr_selected   = { (^"selected") ~ delimiter ~ (string | module) }
attr_label      = { (^"label") ~ delimiter ~ (string | module) }
attr_clip       = { (^"clip") ~ delimiter ~ (boolean | module) }
attr_toggled    = { (^"toggled") ~ delimiter ~ (boolean | module) }
attr_wrapping   = { (^"wrapping") ~ delimiter ~ (glyph | word | none | either | module) }
attr_shaping    = { (^"shaping") ~ delimiter ~ (basic | advanced | module) }
attr_border     = { (^"border") ~ delimiter ~ (border_option_list | module) }
attr_shadow     = { (^"shadow") ~ delimiter ~ (shadow_option_list | module) }
attr_direction  = { (^"direction") ~ delimiter ~ (direction_horizontal | direction_vertical | both | module) }

padding_option_list = _{ padding_option ~ ("," ~ padding_option)* }
padding_option      = _{ option_top | option_bottom | option_left | option_right }

border_option_list = _{ border_option ~ ("," ~ border_option)* }
border_option      = _{ option_color | option_width | option_radius }

shadow_option_list = _{ shadow_option ~ ("," ~ shadow_option)* }
shadow_option      = _{ option_top | option_bottom | option_left | option_right }

option_color    = { (^"color" | ^"colour") ~ "(" ~ color ~ ")" }
option_gradient = { (^"gradient") ~ "(" ~ gradient ~ ")" }
option_width    = { (^"width" | ^"w") ~ "(" ~ float ~ ")" }
option_radius   = { (^"radius") ~ "(" ~ (full | uniform) ~ ")" }
option_top      = { top ~ "(" ~ float ~ ")" }
option_bottom   = { bottom ~ "(" ~ float ~ ")" }
option_left     = { left ~ "(" ~ float ~ ")" }
option_right    = { right ~ "(" ~ float ~ ")" }

// uniform padding
uniform = { float }
edge    = { ((float ~ ",") ~ float) }
full    = { ((float ~ ","){3} ~ float) }

// Length
length       = { fill_portion | fixed | fill | shrink }
fill_portion = { ^"fill-portion" ~ "(" ~ integer ~ ")" }
fixed        = { ^"fixed" ~ "(" ~ float ~ ")" }
fill         = { ^"fill" }
shrink       = { ^"shrink" }

// Pixels
pixels = { float }

// Horizontal Alignment
horizontal = { left | center | right }

// Vertical Alignment
vertical = { top | center | bottom }

// alignment = { top | bottom | left | right | center }

left   = { ^"left" | ^"l" }
right  = { ^"right" | ^"r" }
top    = { ^"top" | ^"t" }
bottom = { ^"bottom" | ^"b" }
center = { ^"center" | ^"centre" | ^"c" }

color_hex_rrggbbaa = _{ ASCII_HEX_DIGIT{8} }
color_hex_rrggbb   = _{ ASCII_HEX_DIGIT{6} }
color_hex_rgba     = _{ ASCII_HEX_DIGIT{4} }
color_hex_rgb      = _{ ASCII_HEX_DIGIT{3} }
color_hex          =  { "#" ~ (color_hex_rrggbbaa | color_hex_rrggbb | color_hex_rgba | color_hex_rgb) }

// Colors are parsed as strings, and passed to ColorParser
color = @{ proxy }
// Gradients are parsed as strings, and passed to GradientParser
gradient = @{ proxy }

// Module
module_name      = @{ (ASCII_ALPHA | "-")* }
module_arguments = @{ (!("{" | "}") ~ ANY)* }
module           =  { module_name ~ "!" ~ "{" ~ module_arguments ~ "}" }

proxy = @{ (!("(" | ")") ~ ANY)* }

// attribute      = { SOI ~ (attributes) ~ EOI }
attribute_list = { SOI ~ (attributes ~ ("," ~ attributes)*) ~ EOI }
