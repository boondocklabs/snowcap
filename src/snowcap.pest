/* Snowcap pest grammar */

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

COMMENT           = _{ SingleLineComment }
SingleLineComment =  { "//" ~ (!"\n" ~ ANY)* }

container = {
    "{" ~ ("<" ~ attributes ~ ">")? ~ (row | column | stack | widget)? ~ "}"
}

row    = { ("row" | "-") ~ (id)? ~ ("<" ~ attributes ~ ">")? ~ element_list }
column = { ("col" | "|") ~ (id)? ~ ("<" ~ attributes ~ ">")? ~ element_list }
stack  = { ("stack" | "^") ~ (id)? ~ ("<" ~ attributes ~ ">")? ~ element_list }

element_list = _{ "[" ~ (widget | row | column | stack | container) ~ ("," ~ (widget | row | column | stack | container))* ~ "]" }

widget = { label ~ (id)? ~ ("<" ~ attributes ~ ">")? ~ "(" ~ (element_value | widget | container | row | column | stack | array)? ~ ")" }
// attributes = { attribute ~ ("," ~ attribute)* }

// Consume everything inside <, > to pass to AttributeParser
attributes = @{ (!("<" | ">") ~ ANY)* }

id = { "#" ~ label }

element_value =  { value }
value         = _{ string | number | boolean | null | data_source }
array         =  { "[" ~ value ~ ("," ~ value)* ~ "]" }

data_source = { label ~ "!" ~ "(" ~ string ~ ")" }

boolean = { "true" | "false" }

null = { "null" }

label = @{ (ASCII_ALPHA | "-")* }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

markup = _{ SOI ~ (container) ~ EOI }
